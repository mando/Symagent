dnl @(#) $Header: /cvs/opensims/agent/library/configure.in,v 1.8 2007/03/12 04:45:18 mikee Exp $ (LBL)
dnl
dnl Copyright (c) 1994, 1995, 1996, 1997
dnl	The Regents of the University of California.  All rights reserved.
dnl
dnl Process this file with autoconf to produce a configure script.
dnl

#####################################################################

AC_PREREQ(2.53)

AC_INIT()

# RULES FOR CHANGING THE VERSION NUMBER
#	1.	If you have changed any of the sources for this library but the
#		interface is unchanged, the GLOBAL_VERSION_BUGFIX must be incremented.
#		This is a new revision of the current interface.
#
#	2.	If the interface has changed, then GLOBAL_VERSION_MAJOR must be incremented,
#		and GLOBAL_VERSION_BUGFIX reset to `0'. This is the first revision of a
#		new interface.
#
#	3.	If the new interface is a superset of the previous interface
#		(that is, if the previous interface has not been broken by the
#		changes in this new release), then GLOBAL_VERSION_MINOR must be
#		incremented. This release is backwards compatible with the previous release.
#
#	4.	If the new interface has removed elements with respect to the
#		previous interface, then you have broken backward compatibility
#		and GLOBAL_VERSION_MINOR must be reset to `0'. This release has a
#		new, but backwards incompatible interface.
#	5.	Note that, whatever you do, you should update GLOBAL_VERSION_NUMBER
#		as well.

GLOBAL_VERSION_MAJOR=1
GLOBAL_VERSION_MINOR=1
GLOBAL_VERSION_BUGFIX=3
GLOBAL_VERSION_STRING="$GLOBAL_VERSION_MAJOR.$GLOBAL_VERSION_MINOR.$GLOBAL_VERSION_BUGFIX"	dnl MM.mm.bb
GLOBAL_VERSION_NUMBER='0x010103L'								dnl MMmmbb
AC_DEFINE_UNQUOTED(LIBSYMBIOT_VERSION_NUMBER,[$GLOBAL_VERSION_NUMBER],[Library version number])

CREATE_CONFIG_FILE='symlib-config.h'		dnl Note duplicate in AC_CONFIG_HEADER

AC_CONFIG_SRCDIR([symlib-api.cc])
AC_CONFIG_AUX_DIR([conftools])
AC_CONFIG_HEADER([symlib-config.h])

sinclude(conftools/ac_c_bigendian_cross.m4)
sinclude(conftools/ac_define_dir.m4)

# Workaround to prevent autoconf from adding its own debugging
# and optimization flags
CXXFLAGS="$CXXFLAGS "

# Set the installation prefix directory
AC_PREFIX_DEFAULT([/usr/local])

AC_CHECK_TOOL_PREFIX

#####################################################################

dnl Determine host system OS type
AC_CANONICAL_TARGET

AC_C_BIGENDIAN_CROSS

dnl Platform-specific Makefile setup
case "$target" in
        *-*-solaris*)   		platform=SOLARIS ;;
        *-*-sco*)       		platform=OPENSERVER ;;
        *-*-sysv5*)     		platform=UNIXWARE ;;
        *-*-UnixWare*)  		platform=UNIXWARE ;;
        *-*-unixware*)  		platform=UNIXWARE ;;
        *-*-beos*)      		platform=BEOS ;;
        *-*-linux*)     		platform=LINUX ;;
        *-*-freebsd*)   		platform=FREEBSD ;;
        *-*-irix*)      		platform=IRIX ;;
        *-*-aix*)       		platform=AIX
								case "$target" in
									*-*-aix4.1*)     osver=AIX41 ;;
									*-*-aix4.2*)     osver=AIX42 ;;
									*-*-aix4.3*)     osver=AIX43 ;;
									*-*-aix4.*)      osver=AIX43 ;;
								esac
								;;
        *-sequent-*)    		platform=PTX ;;
        *-*-hp*)        		platform=HPUX
								case "$target" in
									*-*-hpux11*)  osver=HPUX11 ;;
									*-*-hpux10*)  osver=HPUX10 ;;
									*)            osver=HPUX11 ;;
								esac
								;;
        *-*-mvs*)       		platform=OS390 ;;
        *-*-os400*)     		platform=OS400 ;;
        *-*-OS400*)     		platform=OS400 ;;
        *-*-osf*)       		platform=TRU64 ;;
        *-*-darwin*)			platform=MACOSX
								case "$target" in
									*-*-darwin6*)   osver=MACOSX102 ;;
									*-*-darwin7*)   osver=MACOSX103 ;;
									*)	            osver=MACOSX ;;
								esac
								;;
        *-*-cygwin*)			platform=CYGWIN ;;
        *)              		platform=UNKNOWN ;;
esac
AC_SUBST(platform)
dnl Stick this value into our config.h file as well
AC_DEFINE_UNQUOTED(PLATFORM,["$target"],[The target OS platform])

if test "$platform" = "LINUX"; then
	AC_MSG_CHECKING(if we are on Gentoo Linux)
	if test -e /etc/make.conf; then
		GENTOO_CFLAGS=`grep -v '^\#' /etc/make.conf | grep CFLAGS\=  | sed -e 's/\"//g' | sed -e 's/CFLAGS\=//' | sed -e 's/\-O[[:digit:]]\?//'`
		ac_cv_cc_linuxgentoo_defined=yes
		AC_MSG_RESULT($ac_cv_cc_linuxgentoo_defined)
		if test "$GENTOO_CFLAGS" != ""; then
			CXXFLAGS="$CXXFLAGS $GENTOO_CFLAGS"
		fi
	fi
fi

#####################################################################
# Configuration options
#####################################################################

AC_ARG_ENABLE(gdb,
	[AC_HELP_STRING(--enable-gdb,[Add gdb debugging support to all libraries])],
	[ ENABLE_GDB=$enableval],
	[ ENABLE_GDB=no ]
	)

if test "$ENABLE_GDB" = ""; then
	ENABLE_GDB="yes"
fi

#--------------------------------

AC_ARG_WITH(optimize,
	[AC_HELP_STRING(--with-optimize=LEVEL,[Compiler optimization; can be one of 0, 1, 2, or 3 [2]])],
	[ WITH_OPTIMIZE=$withval ],
	[ WITH_OPTIMIZE=2 ]
	)

if test "$WITH_OPTIMIZE" = ""; then
	WITH_OPTIMIZE=2
fi

#--------------------------------

AC_ARG_WITH(local-expat,
	[AC_HELP_STRING([--with-local-expat],[Always use included expat library, ignoring any installed versions [no]])],
	[ BUILD_EXPAT=$withval ],
	[ BUILD_EXPAT="no" ]
	)

if test "$BUILD_EXPAT" = ""; then
	BUILD_EXPAT="yes"
fi

#--------------------------------

if test "$platform" = "LINUX"; then
    DEFAULT_OPENSSL_DIR='/usr'
else
    DEFAULT_OPENSSL_DIR='/usr'
fi
AC_ARG_WITH(openssl-dir,
	[AC_HELP_STRING([--with-openssl-dir=DIR],[OpenSSL base directory; headers should be in DIR/include/openssl and libraries should be in DIR/lib [/usr]])],
	[ WITH_OPENSSL_DIR=$withval ],
	[ WITH_OPENSSL_DIR=$DEFAULT_OPENSSL_DIR ]
	)

if test "$WITH_OPENSSL_DIR" = ""; then
	WITH_OPENSSL_DIR=$DEFAULT_OPENSSL_DIR
fi

#--------------------------------

DEFAULT_LOG_DIR='/var/log'
AC_ARG_WITH(log-dir,
	[AC_HELP_STRING([--with-log-dir=DIR],[Base directory for log files [/var/log/opensims]])],
	[ WITH_LOG_DIR=$withval ],
	[ WITH_LOG_DIR=$DEFAULT_LOG_DIR ]
	)

if test "$WITH_LOG_DIR" = ""; then
	WITH_LOG_DIR=$DEFAULT_LOG_DIR
fi

#--------------------------------

DEFAULT_CONF_DIR='/etc'
AC_ARG_WITH(conf-dir,
	[AC_HELP_STRING([--with-conf-dir=DIR],[Base directory for configuration files [/etc/opensims]])],
	[ WITH_CONF_DIR=$withval ],
	[ WITH_CONF_DIR=$DEFAULT_CONF_DIR ]
	)

if test "$WITH_CONF_DIR" = ""; then
	WITH_CONF_DIR=$DEFAULT_CONF_DIR
fi

#--------------------------------

DEFAULT_SERVER='localhost'
AC_ARG_WITH(server,
	[AC_HELP_STRING([--with-server=HOSTNAME],[Name or IP address of SIMS server [localhost]])],
	[ WITH_SERVER=$withval ],
	[ WITH_SERVER=$DEFAULT_SERVER ]
	)

if test "$WITH_SERVER" = ""; then
	WITH_SERVER=$DEFAULT_SERVER
fi

#--------------------------------

DEFAULT_CA_CERT=''
AC_ARG_WITH(ca-cert,
	[AC_HELP_STRING([--with-ca-cert=PATH],[Path to an alternate CA certificate to use for secure communications])],
	[ WITH_CA_CERT=$withval ],
	[ WITH_CA_CERT=$DEFAULT_CA_CERT ]
	)

if test "$WITH_CA_CERT" = ""; then
	WITH_CA_CERT=$DEFAULT_CA_CERT
fi

#--------------------------------

DEFAULT_AGENT_CERT=''
AC_ARG_WITH(agent-cert,
	[AC_HELP_STRING([--with-agent-cert=PATH],[Path to an alternate default agent certificate to use for unprovisioned secure communications])],
	[ WITH_AGENT_CERT=$withval ],
	[ WITH_AGENT_CERT=$DEFAULT_AGENT_CERT ]
	)

if test "$WITH_AGENT_CERT" = ""; then
	WITH_AGENT_CERT=$DEFAULT_AGENT_CERT
fi

#####################################################################

# Save our configure arguments

LIBSYMBIOT_CONFIGURE_COMMAND="$0 $@"
AC_DEFINE_UNQUOTED(LIBSYMBIOT_CONFIGURE_COMMAND,["$LIBSYMBIOT_CONFIGURE_COMMAND"],[The command used to configure this library])

#####################################################################

# Locate a program and check that its version is acceptable. This function
# is called later, when trying to determine compiler versions.
#
# AC_PROG_CHECK_VER(var,
#					namelist,
#					version-switch,
#					[version-extract-regexp],
#					version-glob [, do-if-fail])

AC_DEFUN([AC_CHECK_PROG_VER],
[AC_CHECK_PROGS([$1], [$2])
if test -z "[$]$1"; then
  ac_verc_fail=yes
else
  # Found it, now check the version.
  AC_MSG_CHECKING([version of [$]$1])
changequote(<<,>>)dnl
  ac_prog_version=`<<$>>$1 $3 2>&1 ifelse(<<$4>>,,,
                   <<| sed -n 's/^.*patsubst(<<$4>>,/,\/).*$/\1/p'>>)`
  case $ac_prog_version in
    '') ac_prog_version="v. ?.??, bad"; ac_verc_fail=yes;;
    <<$5>>)
changequote([,])dnl
       ac_prog_version="$ac_prog_version, ok"; ac_verc_fail=no;;
    *) ac_prog_version="$ac_prog_version, bad"; ac_verc_fail=yes;;

  esac
  AC_MSG_RESULT([$ac_prog_version])
fi
ifelse([$6],,,
[if test $ac_verc_fail = yes; then
  $6
fi])
])

#####################################################################

dnl Program checks
AC_PROG_CXX

#####################################################################

# Find the compilers and make sure they are the right versions

AC_CHECK_PROG_VER([CC],[gcc cc],[-v],
	[version \([egcygnustpi-]*[0-9.]*\)], [3.[1-9]*] | 4.*,
	[AC_MSG_ERROR([unsupported compiler and/or version; configuration aborting])])

AC_CHECK_PROG_VER([CXX],[g++ cxx cc++ c++],[-v],
	[version \([egcygnustpi-]*[0-9.]*\)], [3.[1-9]*] | 4.*,
	[AC_MSG_ERROR([unsupported compiler and/or version; configuration aborting])])

dnl Check for gcc version 4
ac_gcc_version=`gcc --version | head -n 1 | cut -f3 -d' ' | cut -f1 -d'.'`;
if test $ac_gcc_version = "4"; then
	HAVE_GCC_4=1
else
	HAVE_GCC_4=0
fi
AC_DEFINE_UNQUOTED(GCC_VERS_4,[$HAVE_GCC_4],[Set this to "0" if you don't have gcc Version 4])

AC_LANG(C++)

#####################################################################

# Libtool stuff

sinclude(conftools/libtool.m4)

LIBCURRENT=$GLOBAL_VERSION_MAJOR
LIBAGE=$GLOBAL_VERSION_MINOR
LIBREVISION=$GLOBAL_VERSION_BUGFIX

AC_SUBST(LIBCURRENT)
AC_SUBST(LIBREVISION)
AC_SUBST(LIBAGE)

AM_DISABLE_STATIC

AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

AC_PROG_INSTALL

#####################################################################

# OS-specific adjustments

HAVE_BROKEN_REALPATH="0"
HAVE__DEV_RANDOM="0"
HAVE__DEV_URANDOM="0"
OPTIONAL_OBJECTS=""

case "$platform" in
	MACOSX)
		HAVE_BROKEN_REALPATH="1"
	;;
	CYGWIN)
		LIBS="$LIBS -lstdc++"
	;;
esac

if test -c "/dev/random"; then
	HAVE__DEV_RANDOM=1
fi
if test -c "/dev/urandom"; then
	HAVE__DEV_URANDOM=1
fi

AC_DEFINE_UNQUOTED(HAVE_BROKEN_REALPATH,[$HAVE_BROKEN_REALPATH],[Define to 1 if this platform's realpath() call sometimes changes the current working directory, and to 0 if you don't.])
AC_DEFINE_UNQUOTED(HAVE__DEV_RANDOM,[$HAVE__DEV_RANDOM],[Define to 1 if this platform supports /dev/random, and to 0 if it doesn't.])
AC_DEFINE_UNQUOTED(HAVE__DEV_URANDOM,[$HAVE__DEV_URANDOM],[Define to 1 if this platform supports /dev/urandom, and to 0 if it doesn't.])

#####################################################################

# Standard header checks
AC_HEADER_STDC

# Required headers
AC_CHECK_HEADERS([algorithm cctype cerrno climits cmath cstdarg cstddef cstdio cstdlib cstring dirent.h fcntl.h fnmatch.h grp.h iostream limits.h map memory pwd.h signal.h stdint.h string sys/stat.h sys/types.h sys/wait.h syslog.h unistd.h vector zlib.h],
	[],
	[AC_MSG_ERROR([required header files are missing; configuration aborting])])

# Other headers
AC_HEADER_DIRENT
AC_HEADER_STAT
AC_HEADER_TIME
AC_CHECK_HEADERS([crypt.h ctime dlfcn.h linux/sockios.h net/ethernet.h new ostream sys/poll.h sys/sysinfo.h sys/syslimits.h sys/time.h time.h values.h])

AC_CHECK_HEADERS([sys/sysctl.h],
	[LOCAL_HAVE_SYS_SYSCTL_H=1],
	[LOCAL_HAVE_SYS_SYSCTL_H=0])

AC_CHECK_HEADERS([netdb.h netinet/in.h sys/ioctl.h sys/param.h sys/socket.h],
	[],
	[AC_MSG_ERROR([required header files for TCP/IP object support are missing; configuration aborting])])

AC_CHECK_HEADERS([net/if.h arpa/inet.h],
	[],
	[AC_MSG_ERROR([required header files for TCP/IP object support are missing; configuration aborting])],
	[
		#include <sys/types.h>
		#include <sys/socket.h>
		#ifdef HAVE_NET_IF_H
			#include <net/if.h>
		#endif
	])

AC_CHECK_HEADERS([net/route.h],
	[LOCAL_HAVE_NET_ROUTE_H=1],
	[LOCAL_HAVE_NET_ROUTE_H=0],
	[
		#include <sys/types.h>
		#include <sys/socket.h>
	])

AC_CHECK_HEADERS([net/if_dl.h])

# Optional headers
if test "$BUILD_EXPAT" = "no"; then
	AC_CHECK_HEADERS([expat.h],
		[
			AC_MSG_CHECKING(expat version)
			
			SAVED_CXXFLAGS=$CXXFLAGS
			CXXFLAGS="$CXXFLAGS  -Werror"
			
			AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
								#if defined(inline)
									#undef inline
								#endif
								#include <expat.h>
								
								#if !defined(XML_MAJOR_VERSION) || !defined(XML_MINOR_VERSION) || !defined(XML_MICRO_VERSION)
									#error Incompatible version
								#else
									#if XML_MAJOR_VERSION < 1
										#error Incompatible version
									#else
										#if XML_MAJOR_VERSION == 1 && XML_MINOR_VERSION < 95
											#error Incompatible version
										#else
											#if XML_MAJOR_VERSION == 1 && XML_MINOR_VERSION == 95 && XML_MICRO_VERSION < 7
												#error Incompatible version
											#endif
										#endif
									#endif
								#endif
							  ],
							  [
								return 1;
							  ]),
			  [AC_MSG_RESULT(compatible)],
			  [
				BUILD_EXPAT=yes
				AC_MSG_RESULT([expat reporting bad or missing version (1.95.7 or later required); will build local library])
			  ])
			 
			 CXXFLAGS=$SAVED_CXXFLAGS
		],
		[BUILD_EXPAT=yes])
fi

#####################################################################

# Check for system functions

AC_LANG_PUSH(C)

AC_CHECK_FUNCS([memmove memset],
	[],
	[AC_MSG_ERROR([required function definitions are missing; configuration aborting])])

# Other function checking
AC_CHECK_FUNCS([crypt crypt_r getloadavg lchown localtime_r nanosleep poll readdir_r select setenv sleep sysinfo usleep])
AC_FUNC_FORK
AC_FUNC_STRERROR_R
AC_FUNC_STRFTIME

AC_LANG_POP(C)

#####################################################################

# System library check

# Optional libraries
AC_SEARCH_LIBS([dlopen],[dl c])
AC_SEARCH_LIBS([crypt],[crypt c])

# Required libraries
if test "$BUILD_EXPAT" = "no"; then
	AC_CHECK_LIB([expat],[XML_ParserCreate],
				 [],
				 [BUILD_EXPAT=yes])
fi

AC_CHECK_LIB([z],[gzdopen],
			 [],
			 [AC_MSG_ERROR([required library is missing; configuration aborting])])

#####################################################################

# Check type of socket length argument

AC_MSG_CHECKING([type of socket length argument])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

SOCKET_SIZE_TYPE=none

# First assume that it's a socklen_t type
AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <stdlib.h>
									#include <sys/types.h>
									#include <sys/socket.h>
								  ],
								  [
									int a = accept(1, static_cast<struct sockaddr*>(0), static_cast<socklen_t*>(0));
									return (a != 0);
								  ]),
				  [SOCKET_SIZE_TYPE=socklen_t])

if test "$SOCKET_SIZE_TYPE" = "none"; then
	# Now assume it's a size_t type
	AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
										#if defined(inline)
											#undef inline
										#endif
										#include <stdlib.h>
										#include <sys/types.h>
										#include <sys/socket.h>
									  ],
									  [
										int a = accept(1, static_cast<struct sockaddr*>(0), static_cast<size_t*>(0));
										return (a != 0);
									  ]),
					  [SOCKET_SIZE_TYPE=size_t])
fi

if test "$SOCKET_SIZE_TYPE" = "none"; then
	# We don't know what it is; assume an int
	SOCKET_SIZE_TYPE=int
fi

AC_MSG_RESULT([$SOCKET_SIZE_TYPE])

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(SOCKET_SIZE_TYPE,[$SOCKET_SIZE_TYPE],[The argument type for a socket size])

#####################################################################

# Check the first argument to getrlimit/setrlimit

AC_MSG_CHECKING([base type of first arg to getrlimit/setrlimit])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

RESOURCE_LIMIT_TYPE=none

# First assume that it's a __rlimit_resource type
AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <sys/time.h>
									#include <sys/resource.h>
									#include <unistd.h>
								  ],
								  [
									int a = getrlimit (static_cast<__rlimit_resource>(0),static_cast<struct rlimit*>(NULL));
									return (a != 0);
								  ]),
				  [RESOURCE_LIMIT_TYPE=__rlimit_resource])

if test "$RESOURCE_LIMIT_TYPE" = "none"; then
	# We don't know what it is; assume an int
	RESOURCE_LIMIT_TYPE=int
fi

AC_MSG_RESULT([$RESOURCE_LIMIT_TYPE])

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(RESOURCE_LIMIT_TYPE,[$RESOURCE_LIMIT_TYPE],[The argument type for getrlimit/setrlimit])

#####################################################################

# See if sa_len is defined in struct sockaddr

AC_MSG_CHECKING([if sa_len is defined in struct sockaddr])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

HAVE_SOCKADDR_SA_LEN="0"

AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <sys/types.h>
									#include <sys/socket.h>
								  ],
								  [
									struct sockaddr	s;
									return (s.sa_len == 0);
								  ]),
				  [HAVE_SOCKADDR_SA_LEN="1"])

if test "$HAVE_SOCKADDR_SA_LEN" = "1"; then
	AC_MSG_RESULT([yes])
else
	AC_MSG_RESULT([no])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(HAVE_SOCKADDR_SA_LEN,[$HAVE_SOCKADDR_SA_LEN],[Define to 1 if your struct sockaddr has a sa_len field, and to 0 if you don't.])

#####################################################################

# See if SIOCGIFHWADDR is defined

AC_MSG_CHECKING([if SIOCGIFHWADDR is defined])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

HAVE_DECL_SIOCGIFHWADDR="0"

AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#if HAVE_LINUX_SOCKIOS_H
										#include <linux/sockios.h>
									#endif
									#include <sys/socket.h>
								  ],
								  [
									int	a = SIOCGIFHWADDR;
									return (a != 0);
								  ]),
				  [HAVE_DECL_SIOCGIFHWADDR="1"])

if test "$HAVE_DECL_SIOCGIFHWADDR" = "1"; then
	AC_MSG_RESULT([yes])
else
	if test "$LOCAL_HAVE_SYS_SYSCTL_H" != "1"; then
		AC_MSG_ERROR([not defined and required due to sys/sysctl.h absence])
	else
		if test "$LOCAL_HAVE_NET_ROUTE_H" != "1"; then
			AC_MSG_ERROR([not defined and required due to net/route.h absence])
		else
			AC_MSG_RESULT([no])
		fi
	fi
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(HAVE_DECL_SIOCGIFHWADDR,[$HAVE_DECL_SIOCGIFHWADDR],[Define to 1 if you SIOCGIFHWADDR defined and to 0 if you don't.])

#####################################################################

# See if PTHREAD_CANCEL_DEFERRED is defined

AC_MSG_CHECKING([if PTHREAD_CANCEL_DEFERRED is defined])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

HAVE_DECL_PTHREAD_CANCEL_DEFERRED="0"

AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <pthread.h>
								  ],
								  [
									int	a = PTHREAD_CANCEL_DEFERRED;
									return (a != 0);
								  ]),
				  [HAVE_DECL_PTHREAD_CANCEL_DEFERRED="1"])

if test "$HAVE_DECL_PTHREAD_CANCEL_DEFERRED" = "1"; then
	AC_MSG_RESULT([yes])
else
	AC_MSG_RESULT([no])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(HAVE_DECL_PTHREAD_CANCEL_DEFERRED,[$HAVE_DECL_PTHREAD_CANCEL_DEFERRED],[Define to 1 if you PTHREAD_CANCEL_DEFERRED defined and to 0 if you don't.])

#####################################################################

# See if PTHREAD_CANCEL_ASYNCHRONOUS is defined

AC_MSG_CHECKING([if PTHREAD_CANCEL_ASYNCHRONOUS is defined])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

HAVE_DECL_PTHREAD_CANCEL_ASYNCHRONOUS="0"

AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <pthread.h>
								  ],
								  [
									int	a = PTHREAD_CANCEL_ASYNCHRONOUS;
									return (a != 0);
								  ]),
				  [HAVE_DECL_PTHREAD_CANCEL_ASYNCHRONOUS="1"])

if test "$HAVE_DECL_PTHREAD_CANCEL_ASYNCHRONOUS" = "1"; then
	AC_MSG_RESULT([yes])
else
	AC_MSG_RESULT([no])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(HAVE_DECL_PTHREAD_CANCEL_ASYNCHRONOUS,[$HAVE_DECL_PTHREAD_CANCEL_ASYNCHRONOUS],[Define to 1 if you PTHREAD_CANCEL_DEFERRED defined and to 0 if you don't.])

#####################################################################

# See if PTHREAD_CANCEL_ENABLE is defined

AC_MSG_CHECKING([if PTHREAD_CANCEL_ENABLE is defined])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

HAVE_DECL_PTHREAD_CANCEL_ENABLE="0"

AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <pthread.h>
								  ],
								  [
									int	a = PTHREAD_CANCEL_ENABLE;
									return (a != 0);
								  ]),
				  [HAVE_DECL_PTHREAD_CANCEL_ENABLE="1"])

if test "$HAVE_DECL_PTHREAD_CANCEL_ENABLE" = "1"; then
	AC_MSG_RESULT([yes])
else
	AC_MSG_RESULT([no])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(HAVE_DECL_PTHREAD_CANCEL_ENABLE,[$HAVE_DECL_PTHREAD_CANCEL_ENABLE],[Define to 1 if you PTHREAD_CANCEL_DEFERRED defined and to 0 if you don't.])

#####################################################################

# See if PTHREAD_CANCEL_DISABLE is defined

AC_MSG_CHECKING([if PTHREAD_CANCEL_DISABLE is defined])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

HAVE_DECL_PTHREAD_CANCEL_DISABLE="0"

AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <pthread.h>
								  ],
								  [
									int	a = PTHREAD_CANCEL_DISABLE;
									return (a != 0);
								  ]),
				  [HAVE_DECL_PTHREAD_CANCEL_DISABLE="1"])

if test "$HAVE_DECL_PTHREAD_CANCEL_DISABLE" = "1"; then
	AC_MSG_RESULT([yes])
else
	AC_MSG_RESULT([no])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(HAVE_DECL_PTHREAD_CANCEL_DISABLE,[$HAVE_DECL_PTHREAD_CANCEL_DISABLE],[Define to 1 if you PTHREAD_CANCEL_DEFERRED defined and to 0 if you don't.])

#####################################################################

# Check layout of struct stat

AC_CHECK_MEMBER([struct stat.st_atimespec],
				[
					STAT_TIME_TYPE='struct timespec'
					STAT_TIME_ATIME=st_atimespec
					STAT_TIME_MTIME=st_mtimespec
					STAT_TIME_CTIME=st_ctimespec
				],
				[
					AC_CHECK_MEMBER([struct stat.st_atime],
									[
										STAT_TIME_TYPE=time_t
										STAT_TIME_ATIME=st_atime
										STAT_TIME_MTIME=st_mtime
										STAT_TIME_CTIME=st_ctime
									],
									[
										AC_MSG_ERROR([unable to determine struct stat configuration; configuration aborting])
									],
									[
										#include <sys/types.h>
										#include <sys/stat.h>
									])
				],
				[
					#include <sys/types.h>
					#include <sys/stat.h>
				])

AC_DEFINE_UNQUOTED(STAT_TIME_TYPE,[$STAT_TIME_TYPE],[Data type of time fields within stat struct])
AC_DEFINE_UNQUOTED(STAT_TIME_ATIME,[$STAT_TIME_ATIME],[Name of time of last access field in struct stat])
AC_DEFINE_UNQUOTED(STAT_TIME_MTIME,[$STAT_TIME_MTIME],[Name of time of last modification field in struct stat])
AC_DEFINE_UNQUOTED(STAT_TIME_CTIME,[$STAT_TIME_CTIME],[Name of time of last change field in struct stat])

#####################################################################

# Pthread support

HAVE_PTHREAD_KILL=0
PTHREAD_T_IS_OPAQUE=

AC_CHECK_HEADERS([pthread.h semaphore.h],
	[],
	[AC_MSG_ERROR([required pthread header files are missing; configuration aborting])])
AC_CHECK_HEADERS([thread.h])

case "$platform" in
	FREEBSD)
		LDFLAGS="$LDFLAGS -pthread"
	;;
	*)
		AC_CHECK_LIB([pthread],[pthread_create],[],[AC_MSG_ERROR([required library is missing; configuration aborting])],[])
	;;
esac

AC_MSG_CHECKING([to see if pthread_kill is defined])
AC_EGREP_HEADER(pthread_kill,pthread.h,[HAVE_PTHREAD_KILL=1],[HAVE_PTHREAD_KILL=0])
if test "$HAVE_PTHREAD_KILL" = "1"; then
	AC_MSG_RESULT([yes])
else
	AC_MSG_RESULT([no])
fi

AC_MSG_CHECKING([opacity of pthread_t])

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

PTHREAD_T_IS_OPAQUE="1"
AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <pthread.h>
								  ],
								  [
									unsigned long a = pthread_self();
									return (a != 0);
								  ]),
				  [PTHREAD_T_IS_OPAQUE="0"])

if test "$PTHREAD_T_IS_OPAQUE" = "1"; then
	AC_MSG_RESULT([opaque])
else
	AC_MSG_RESULT([just a number])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(HAVE_PTHREAD_KILL,[$HAVE_PTHREAD_KILL],[Set to 1 if pthread_kill is supported.])
AC_DEFINE_UNQUOTED(PTHREAD_T_IS_OPAQUE,[$PTHREAD_T_IS_OPAQUE],[Set to 1 if pthread_t is opaque, 0 if it is an integer.])

#####################################################################

AC_MSG_CHECKING([if we should add gdb debugging support])

if test "$ENABLE_GDB" = "yes"; then
	AC_MSG_RESULT([yes])
	CXXFLAGS="$CXXFLAGS -g"
else
	LDFLAGS="$LDFLAGS -s"
	AC_MSG_RESULT([no])
fi

#####################################################################

AC_MSG_CHECKING([whether to build the local expat library])
AC_MSG_RESULT($BUILD_EXPAT)

#####################################################################

AC_MSG_CHECKING([optimization level])

AC_MSG_RESULT([$WITH_OPTIMIZE])
CXXFLAGS="$CXXFLAGS -O$WITH_OPTIMIZE"

#####################################################################

AC_MSG_CHECKING([for alternate CA certificate file])

if test "$WITH_CA_CERT" = ""; then
	AC_MSG_RESULT([no])
else
	if test -e "$WITH_CA_CERT"; then
		AC_MSG_RESULT([$WITH_CA_CERT])
	else
		AC_MSG_RESULT([$WITH_CA_CERT not found; using included certificate])
		$WITH_CA_CERT=''
	fi
fi

#####################################################################

AC_MSG_CHECKING([for alternate default agent certificate file])

if test "$WITH_AGENT_CERT" = ""; then
	AC_MSG_RESULT([no])
else
	if test -e "$WITH_AGENT_CERT"; then
		AC_MSG_RESULT([$WITH_AGENT_CERT])
	else
		AC_MSG_RESULT([$WITH_AGENT_CERT not found; using included certificate])
		$WITH_AGENT_CERT=''
	fi
fi

#####################################################################

# OpenSSL base directory

AC_CHECK_FILES([$WITH_OPENSSL_DIR/include/openssl/crypto.h $WITH_OPENSSL_DIR/include/openssl/err.h $WITH_OPENSSL_DIR/include/openssl/evp.h $WITH_OPENSSL_DIR/include/openssl/conf.h $WITH_OPENSSL_DIR/include/openssl/opensslv.h $WITH_OPENSSL_DIR/include/openssl/pem.h $WITH_OPENSSL_DIR/include/openssl/rand.h $WITH_OPENSSL_DIR/include/openssl/ssl.h $WITH_OPENSSL_DIR/include/openssl/x509v3.h],
	[],
	[AC_MSG_ERROR([required OpenSSL header files are missing; configuration aborting])])

AC_CHECK_FILE([$WITH_OPENSSL_DIR/include/openssl/blowfish.h],
	[HAVE_OPENSSL_BLOWFISH_H=1],
	[HAVE_OPENSSL_BLOWFISH_H=0])
AC_DEFINE_UNQUOTED(HAVE_OPENSSL_BLOWFISH_H,[$HAVE_OPENSSL_BLOWFISH_H],[Set to 1 if you have Blowfish, 0 otherwise])

AC_CHECK_FILE([$WITH_OPENSSL_DIR/include/openssl/cast.h],
	[HAVE_OPENSSL_CAST_H=1],
	[HAVE_OPENSSL_CAST_H=0])
AC_DEFINE_UNQUOTED(HAVE_OPENSSL_CAST_H,[$HAVE_OPENSSL_CAST_H],[Set to 1 if you have CAST, 0 otherwise])

AC_CHECK_FILE([$WITH_OPENSSL_DIR/include/openssl/des.h],
	[HAVE_OPENSSL_DES_H=1],
	[HAVE_OPENSSL_DES_H=0])
AC_DEFINE_UNQUOTED(HAVE_OPENSSL_DES_H,[$HAVE_OPENSSL_DES_H],[Set to 1 if you have DES, 0 otherwise])

AC_CHECK_FILE([$WITH_OPENSSL_DIR/include/openssl/idea.h],
	[HAVE_OPENSSL_IDEA_H=1],
	[HAVE_OPENSSL_IDEA_H=0])
AC_DEFINE_UNQUOTED(HAVE_OPENSSL_IDEA_H,[$HAVE_OPENSSL_IDEA_H],[Set to 1 if you have IDEA, 0 otherwise])

AC_CHECK_FILE([$WITH_OPENSSL_DIR/include/openssl/rc2.h],
	[HAVE_OPENSSL_RC2_H=1],
	[HAVE_OPENSSL_RC2_H=0])
AC_DEFINE_UNQUOTED(HAVE_OPENSSL_RC2_H,[$HAVE_OPENSSL_RC2_H],[Set to 1 if you have RC2, 0 otherwise])

AC_CHECK_FILE([$WITH_OPENSSL_DIR/include/openssl/rc4.h],
	[HAVE_OPENSSL_RC4_H=1],
	[HAVE_OPENSSL_RC4_H=0])
AC_DEFINE_UNQUOTED(HAVE_OPENSSL_RC4_H,[$HAVE_OPENSSL_RC4_H],[Set to 1 if you have RC4, 0 otherwise])

AC_CHECK_FILE([$WITH_OPENSSL_DIR/include/openssl/rc5.h],
	[HAVE_OPENSSL_RC5_H=1],
	[HAVE_OPENSSL_RC5_H=0])
AC_DEFINE_UNQUOTED(HAVE_OPENSSL_RC5_H,[$HAVE_OPENSSL_RC5_H],[Set to 1 if you have RC5, 0 otherwise])

if test "$WITH_OPENSSL_DIR" != "/usr"; then
	INCLUDES="$INCLUDES -I$WITH_OPENSSL_DIR/include"
fi

if test "$WITH_OPENSSL_DIR" != "/usr"; then
	LIBS="$LIBS -L$WITH_OPENSSL_DIR/lib"
fi

# See if we have the directory /usr/kerberos/include; if we do then we're probably
# running on a Red Hat system and we need to include that directory in our search
# path (the default OpenSSL configuration in Red Hat uses stuff in there)

if test "-d /usr/kerberos/include"; then
	INCLUDES="$INCLUDES -I/usr/kerberos/include"
fi

LIBS="$LIBS -lcrypto -lssl"

AC_MSG_CHECKING(OpenSSL version)

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <$WITH_OPENSSL_DIR/include/openssl/opensslv.h>
									
									#if !defined(OPENSSL_VERSION_NUMBER) || (OPENSSL_VERSION_NUMBER < 0x0090603fL)
										#error Incompatible version
									#endif
								  ],
								  [
									return 1;
								  ]),
				  [AC_MSG_RESULT(compatible)],
				  [AC_MSG_ERROR([OpenSSL reporting bad or missing version (0.9.6c or later required); configuration aborting])])

CXXFLAGS=$SAVED_CXXFLAGS

#####################################################################

# Check type of a buffer pointer when dealing with RSA keys

AC_MSG_CHECKING(type of RSA key buffer pointers)

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

# Assume it's a const unsigned char*
RSA_KEY_BUFFER_PTR_IS_CONST=0

# Test to see if it's const
AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <$WITH_OPENSSL_DIR/include/openssl/rsa.h>
								  ],
								  [
									RSA* a = d2i_RSAPublicKey(static_cast<RSA**>(NULL),static_cast<const unsigned char**>(NULL),static_cast<long>(0));
									return (a != NULL);
								  ]),
				  [RSA_KEY_BUFFER_PTR_IS_CONST=1])

if test $RSA_KEY_BUFFER_PTR_IS_CONST = 1; then
	AC_MSG_RESULT([const unsigned char*])
else
	AC_MSG_RESULT([unsigned char*])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(RSA_KEY_BUFFER_PTR_IS_CONST,[$RSA_KEY_BUFFER_PTR_IS_CONST],[Set to 1 if RSA key buffer pointers (as used in d2i_RSAPublicKey for example) are constant, 0 otherwise])

#####################################################################

# Check type of a buffer pointer when dealing with DSA keys

AC_MSG_CHECKING(type of DSA key buffer pointers)

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

# Assume it's not a const unsigned char*
DSA_KEY_BUFFER_PTR_IS_CONST=0

# Test to see if it's const
AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <$WITH_OPENSSL_DIR/include/openssl/dsa.h>
								  ],
								  [
									DSA* a = d2i_DSAPublicKey(static_cast<DSA**>(NULL),static_cast<const unsigned char**>(NULL),static_cast<long>(0));
									return (a != NULL);
								  ]),
				  [DSA_KEY_BUFFER_PTR_IS_CONST=1])

if test $DSA_KEY_BUFFER_PTR_IS_CONST = 1; then
	AC_MSG_RESULT([const unsigned char*])
else
	AC_MSG_RESULT([unsigned char*])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(DSA_KEY_BUFFER_PTR_IS_CONST,[$DSA_KEY_BUFFER_PTR_IS_CONST],[Set to 1 if DSA key buffer pointers (as used in d2i_DSAPublicKey for example) are constant, 0 otherwise])

#####################################################################

# Check type of a buffer pointer when dealing with DH parameters

AC_MSG_CHECKING(type of DH parameter buffer pointers)

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

# Assume it's not a const unsigned char*
DH_PARAM_BUFFER_PTR_IS_CONST=0

# Test to see if it's const
AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <$WITH_OPENSSL_DIR/include/openssl/dh.h>
								  ],
								  [
									DH* a = d2i_DHparams(static_cast<DH**>(NULL),static_cast<const unsigned char**>(NULL),static_cast<long>(0));
									return (a != NULL);
								  ]),
				  [DH_PARAM_BUFFER_PTR_IS_CONST=1])

if test $DH_PARAM_BUFFER_PTR_IS_CONST = 1; then
	AC_MSG_RESULT([const unsigned char*])
else
	AC_MSG_RESULT([unsigned char*])
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(DH_PARAM_BUFFER_PTR_IS_CONST,[$DH_PARAM_BUFFER_PTR_IS_CONST],[Set to 1 if DH parameter buffer pointers (as used in d2i_DHparams for example) are constant, 0 otherwise])

#####################################################################

# Check argument type for SSL info callback get/set functions

AC_MSG_CHECKING(argument type for SSL info callback get/set functions)

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

# Assume it's the fully-qualified version
SSL_INFO_CALLBACK_ARG_WITH_ARGS=1
SSL_INFO_CALLBACK_ARG_ELIDED=0

# Test to see if the older (non-argument) version
AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <$WITH_OPENSSL_DIR/include/openssl/ssl.h>
									
									typedef			void (*SSLInfoCallback) ();
								  ],
								  [
									SSL_set_info_callback(reinterpret_cast<SSL*>(NULL),reinterpret_cast<SSLInfoCallback>(NULL));
									return 0;
								  ]),
				  [SSL_INFO_CALLBACK_ARG_WITH_ARGS=0])

if test $SSL_INFO_CALLBACK_ARG_WITH_ARGS = 1; then
	AC_MSG_RESULT([needs arguments])
else
	case "$OS_CATEGORY" in
		macosx)
			# Mac OS X systems that claim to not need arguments are running an older version
			# of OpenSSL.  Under gcc 3.x, that causes difficulties with the callback definition
			# for the SSL_set_info_callback function (SSL_CTX_set_info_callback is fine).
			# Strangely enough, the callback must have elided (...) arguments in the definition.
			# There's no good reason for this.
			AC_MSG_RESULT([elided arguments])
			SSL_INFO_CALLBACK_ARG_ELIDED=1
		;;
		*)
			AC_MSG_RESULT([no arguments])
		;;
	esac
fi

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(SSL_INFO_CALLBACK_ARG_WITH_ARGS,[$SSL_INFO_CALLBACK_ARG_WITH_ARGS],[Set to 1 if the info callback function used in SSL_set_info_callback and SSL_CTX_set_info_callback requires arguments in the prototype; set to 0 if the prototype has no arguments])
AC_DEFINE_UNQUOTED(SSL_INFO_CALLBACK_ARG_ELIDED,[$SSL_INFO_CALLBACK_ARG_ELIDED],[Set to 1 if the info callback function used in SSL_set_info_callback specifically requires elided (...) arguments; set to 0 otherwise.  Note that SSL_INFO_CALLBACK_ARG_WITH_ARGS must be 0 for this option to be recognized.])


#####################################################################

# Check type of RAND_METHOD used in RAND_set_* and RAND_get_* calls

AC_MSG_CHECKING(type of RAND_METHOD used in RAND_set_* and RAND_get_* calls)

SAVED_CXXFLAGS=$CXXFLAGS
CXXFLAGS="$CXXFLAGS  -Werror"

# Assume it's a const RAND_METHOD
RAND_METHOD_TYPE='const RAND_METHOD'

# Test to see if it's not const
AC_COMPILE_IFELSE(AC_LANG_PROGRAM([
									#if defined(inline)
										#undef inline
									#endif
									#include <$WITH_OPENSSL_DIR/include/openssl/rand.h>
								  ],
								  [
								  	RAND_METHOD* a = RAND_get_rand_method();
									return (a != NULL);
								  ]),
				  [RAND_METHOD_TYPE='RAND_METHOD'])

AC_MSG_RESULT([$RAND_METHOD_TYPE])

CXXFLAGS=$SAVED_CXXFLAGS

AC_DEFINE_UNQUOTED(RAND_METHOD_TYPE,[$RAND_METHOD_TYPE],[The argument type (const or not) of the RAND_METHOD argument and return value in RAND_get_* and RAND_set_* methods])

#####################################################################

# Makefile variable substitution

AC_SUBST(INCLUDES)

EXPAT_SUBDIR='expat-1.95.7'
if test "$BUILD_EXPAT" = "yes"; then
	EXPAT_OBJECTS='$(top_srcdir)/$(EXPAT_SUBDIR)/lib/*.lo'
	OPTIONAL_OBJECTS="$OPTIONAL_OBJECTS $EXPAT_OBJECTS"
	EXPAT_INC='-I$(top_srcdir)/$(EXPAT_SUBDIR)/lib'
	INCLUDES="$EXPAT_INC $INCLUDES"		dnl -- Note that we're making that first
	AC_CONFIG_SUBDIRS(expat-1.95.7)
fi
AC_SUBST(OPTIONAL_OBJECTS)
AC_SUBST(EXPAT_SUBDIR)
AC_SUBST(BUILD_EXPAT)
AC_SUBST(EXTERNAL_CA_CERT,[$WITH_CA_CERT])
AC_SUBST(EXTERNAL_AGENT_CERT,[$WITH_AGENT_CERT])

#####################################################################

# Configuration file substitution

AC_SUBST(SYMBIOT_LOG_DIR_PREFIX,$WITH_LOG_DIR)
AC_SUBST(SYMBIOT_CONF_DIR_PREFIX,$WITH_CONF_DIR)
AC_SUBST(SYMBIOT_SERVER_ADDR,$WITH_SERVER)

ABS_SYSCONFDIR=$WITH_CONF_DIR
AC_DEFINE_UNQUOTED(ABS_SYSCONFDIR,["$ABS_SYSCONFDIR"],[Configuration directory prefix])
AC_SUBST(ABS_SYSCONFDIR)

#####################################################################
AC_CONFIG_FILES([Makefile])
AC_OUTPUT()
